
Функция ОтправитьВопрос(Вопрос, СтруктураПараметров) Экспорт
	
	ИмяСервера = СтруктураПараметров.ИмяСервера;
	
	Соединение = Новый HTTPСоединение(ИмяСервера);
	HTTPЗапрос = СоздатьЗапрос(ДанныеЗапроса(Вопрос, СтруктураПараметров), СтруктураПараметров);
	Ответ = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	Если Не Ответ.КодСостояния = 200 Тогда
		ТекстИсключения = СтрШаблон("Не удалось получить ответ по причине: %1!",
			Ответ.ПолучитьТелоКакСтроку());
		ВызватьИсключение(ТекстИсключения);
	КонецЕсли;
	Возврат ОтветНаВопрос(Ответ);
	
КонецФункции

Функция ДанныеЗапроса(Вопрос, СтруктураПараметров)
	
	Пользователь = СтруктураПараметров.Пользователь;
	LLMМодель = СтруктураПараметров.LLMМодель;
	
	ДанныеВопроса = Новый Структура;
	ДанныеВопроса.Вставить("role", Пользователь);
	ДанныеВопроса.Вставить("content", Вопрос);
	
	Вопросы = Новый Массив;
	Вопросы.Добавить(ДанныеВопроса);
	
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("model", LLMМодель);
	ДанныеЗапроса.Вставить("messages", Вопросы);
	Возврат ДанныеЗапроса;
	
КонецФункции

Функция СоздатьЗапрос(ДанныеВопроса, СтруктураПараметров)
	
	АдресРесурса = СтруктураПараметров.АдресРесурса;
	КлючAPI = СтруктураПараметров.КлючAPI;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Authorization", Authorization(КлючAPI));
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(ОбщегоНазначения.СтрокаJSONИзЗначения(ДанныеВопроса));
	Возврат HTTPЗапрос;
	
КонецФункции

Функция Authorization(КлючAPI)
	
	Возврат "Bearer " + КлючAPI;
	
КонецФункции

Функция ОтветНаВопрос(Ответ)
	
	ОтветТекст = Ответ.ПолучитьТелоКакСтроку();
	ОтветТекст = СтрЗаменить(ОтветТекст, "response_token/s", "response_token");
	ОтветТекст = СтрЗаменить(ОтветТекст, "prompt_token/s", "prompt_token");
	ДанныеОтвета = ОбщегоНазначения.ЗначениеИзJSONСтроки(ОтветТекст);
	Если ДанныеОтвета.choices.Количество() = 0 Тогда
		ОтветНаВопрос = "Ошибка! Сервер обработал вопрос, но вернул пустой ответ!";
	Иначе
		ОтветНаВопрос = ДанныеОтвета.choices[0].message.content;
	КонецЕсли;
	Возврат ОтветНаВопрос;
	
КонецФункции
